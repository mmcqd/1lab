<!--
```agda
open import Cat.Prelude

open import Data.Id.Base
open import Data.Dec

import Cat.Reasoning
```
-->

```agda
module Cat.Instances.Discrete where
```

<!--
```agda
private variable
  â„“ â„“' : Level
  X : Type â„“
  C : Precategory â„“ â„“'

open Precategory
open Functor
open _=>_
```
-->

# Discrete categories {defines="discrete-category"}

Given a groupoid $A$, we can build the category $\rm{Disc}(A)$ with
space of objects $A$ and a single morphism $x \to y$ whenever $x \equiv
y$.

```agda
Disc : (A : Type â„“) â†’ is-groupoid A â†’ Precategory â„“ â„“
Disc A A-grpd .Ob = A
Disc A A-grpd .Hom = _â‰¡_
Disc A A-grpd .Hom-set = A-grpd
Disc A A-grpd .id = refl
Disc A A-grpd ._âˆ˜_ p q = q âˆ™ p
Disc A A-grpd .idr _ = âˆ™-idl _
Disc A A-grpd .idl _ = âˆ™-idr _
Disc A A-grpd .assoc _ _ _ = sym (âˆ™-assoc _ _ _)

-- Disc' : Set â„“ â†’ Precategory â„“ â„“
-- Disc' A = Disc âˆ£ A âˆ£ h where abstract
--   h : is-groupoid âˆ£ A âˆ£
--   h = is-hlevel-suc 2 (A .is-tr)

Disc! : {T : Type â„“} â¦ƒ _ : Underlying T â¦„ (A : T) {@(tactic hlevel-tactic-worker) A-grpd : is-groupoid âŒ A âŒŸ} â†’ Precategory _ _
Disc! A {A-grpd} = Disc âŒ A âŒŸ h where abstract
  h : is-groupoid âŒ A âŒŸ
  h = A-grpd

-- open import Data.Bool

-- ğŸš : âˆ€ â„“ â†’ Set â„“
-- ğŸš â„“ = el! (Lift â„“ Bool)

-- test : âˆ€ {â„“} â†’ Disc! (ğŸš â„“) {A-grpd = {!   !}} â‰¡ Disc! (ğŸš â„“) {A-grpd = {!   !}}
-- test = refl

```

We can lift any function between the underlying types to a functor
between discrete categories. This is because every function
automatically respects equality in a functorial way.

```agda
lift-disc
  : {Tâ‚ : Type â„“} â¦ƒ _ : Underlying Tâ‚ â¦„ {A : Tâ‚} {@(tactic hlevel-tactic-worker) A-grpd : is-groupoid âŒ A âŒŸ } 
    {Tâ‚‚ : Type â„“'} â¦ƒ _ : Underlying Tâ‚‚ â¦„ {B : Tâ‚‚} {@(tactic hlevel-tactic-worker) B-grpd : is-groupoid âŒ B âŒŸ}
  â†’ (âŒ A âŒŸ â†’ âŒ B âŒŸ)
  â†’ Functor (Disc! A {A-grpd}) (Disc! B {B-grpd})
lift-disc f .Fâ‚€ = f
lift-disc f .Fâ‚ = ap f
lift-disc f .F-id = refl
lift-disc f .F-âˆ˜ p q = ap-âˆ™ f q p
```

<!--
```agda
Codisc' : âˆ€ {â„“'} â†’ Type â„“ â†’ Precategory â„“ â„“'
Codisc' x .Ob = x
Codisc' x .Hom _ _ = Lift _ âŠ¤
Codisc' x .Hom-set _ _ = is-propâ†’is-set (Î» _ _ i â†’ lift tt)
Codisc' x .id = lift tt
Codisc' x ._âˆ˜_ _ _ = lift tt
Codisc' x .idr _ = refl
Codisc' x .idl _ = refl
Codisc' x .assoc _ _ _ = refl
```
-->

## Diagrams in Disc(X)

If $X$ is a `discrete type`{.Agda ident=Discrete}, then it is in
particular in `Set`{.Agda}, and we can make diagrams of shape
$\rm{Disc}(X)$ in some category $\cC$, using the decidable
equality on $X$. We note that the decidable equality is _redundant_
information: The construction `Disc'`{.Agda} above extends into a [[left
adjoint]] to the `Ob`{.Agda} functor.

```agda
Disc-diagram
  : {T : Type â„“} â¦ƒ _ : Underlying T â¦„ {X : T} {@(tactic hlevel-tactic-worker) X-grpd : is-groupoid âŒ X âŒŸ} â¦ƒ _ : Discrete âŒ X âŒŸ â¦„
  â†’ (âŒ X âŒŸ â†’ Ob C)
  â†’ Functor (Disc! X {X-grpd}) C
Disc-diagram {C = C} {X = X} â¦ƒ d â¦„ f = F where
  module C = Precategory C

  P : âŒ X âŒŸ â†’ âŒ X âŒŸ â†’ Type _
  P x y = C.Hom (f x) (f y)

  go : âˆ€ {x y : âŒ X âŒŸ} â†’ x â‰¡ y â†’ Dec (x â‰¡áµ¢ y) â†’ P x y
  go {x} {.x} p (yes refláµ¢) = C.id
  go {x} {y}  p (no Â¬p)     = absurd (Â¬p (Idâ‰ƒpath.from p))
```

The object part of the functor is the provided $f : X \to
\rm{Ob}(\cC)$, and the decidable equality is used to prove that
$f$ respects equality. This is why it's redundant: $f$ automatically
respects equality, because it's a function! However, by using the
decision procedure, we get better computational behaviour: Very often,
$\rm{disc}(x,x)$ will be $\rm{yes}(\refl)$, and
substitution along $\refl$ is easy to deal with.

```agda
  F : Functor _ _
  F .Fâ‚€ = f
  F .Fâ‚ {x} {y} p = go p (x â‰¡áµ¢? y)
```

Proving that our our $F_1$ is functorial involves a bunch of tedious
computations with equalities and a whole waterfall of absurd cases:

```agda
  F .F-id {x} = refl
  F .F-âˆ˜  {x} {y} {z} f g =
    J (Î» y g â†’ âˆ€ {z} (f : y â‰¡ z) â†’ go (g âˆ™ f) (x â‰¡áµ¢? z) â‰¡ go f (y â‰¡áµ¢? z) C.âˆ˜ go g (x â‰¡áµ¢? y))
      (Î» f â†’ J (Î» z f â†’ go (refl âˆ™ f) (x â‰¡áµ¢? z) â‰¡ go f (x â‰¡áµ¢? z) C.âˆ˜ C.id) (sym (C.idr _)) f)
      g f
```

<!--
```
Disc-adjunct : 
  {T : Type â„“} â¦ƒ _ : Underlying T â¦„ {X : T} {@(tactic hlevel-tactic-worker) X-grpd : is-groupoid âŒ X âŒŸ}
  â†’ (âŒ X âŒŸ â†’ C .Ob)
  â†’ Functor (Disc! X {X-grpd}) C
Disc-adjunct {C = C} F .Fâ‚€ = F
Disc-adjunct {C = C} F .Fâ‚ p = subst (C .Hom (F _) âŠ™ F) p (C .id)
Disc-adjunct {C = C} F .F-id = transport-refl _
Disc-adjunct {C = C} {T = T} {X-grpd = X-grpd} F .F-âˆ˜ {x} {y} {z} f g = path where
  import Cat.Reasoning C as C
  go = Disc-adjunct {C = C} {T = T} {X-grpd = X-grpd} F .Fâ‚
  abstract
    path : go (g âˆ™ f) â‰¡ C ._âˆ˜_ (go f) (go g)
    path =
      J' (Î» y z f â†’ âˆ€ {x} (g : x â‰¡ y) â†’ go (g âˆ™ f) â‰¡ go f C.âˆ˜ go g)
        (Î» x g â†’ subst-âˆ™ (C .Hom (F _) âŠ™ F) _ _ _
              Â·Â· transport-refl _
              Â·Â· C.introl (transport-refl _))
        f {x} g
```
-->

<!--
```agda
Disc-natural
  : {T : Type â„“} â¦ƒ _ : Underlying T â¦„ {X : T} {@(tactic hlevel-tactic-worker) X-grpd : is-groupoid âŒ X âŒŸ}
  â†’ {F G : Functor (Disc! X {X-grpd}) C}
  â†’ (âˆ€ x â†’ C .Hom (F .Fâ‚€ x) (G .Fâ‚€ x))
  â†’ F => G
Disc-natural fam .Î· = fam
Disc-natural {C = C} {F = F} {G = G} fam .is-natural x y f =
  J (Î» y p â†’ fam y C.âˆ˜ F .Fâ‚ p â‰¡ (G .Fâ‚ p C.âˆ˜ fam x))
    (C.elimr (F .F-id) âˆ™ C.introl (G .F-id))
    f
  where module C = Cat.Reasoning C
```
-->
